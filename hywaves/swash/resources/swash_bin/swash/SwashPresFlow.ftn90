subroutine SwashPresFlow
!
!   --|-----------------------------------------------------------|--
!     | Delft University of Technology                            |
!     | Faculty of Civil Engineering                              |
!     | Environmental Fluid Mechanics Section                     |
!     | P.O. Box 5048, 2600 GA  Delft, The Netherlands            |
!     |                                                           |
!     | Programmers: The SWASH team                               |
!   --|-----------------------------------------------------------|--
!
!
!     SWASH (Simulating WAves till SHore); a non-hydrostatic wave-flow model
!     Copyright (C) 2010-2019  Delft University of Technology
!
!     This program is free software; you can redistribute it and/or
!     modify it under the terms of the GNU General Public License as
!     published by the Free Software Foundation; either version 2 of
!     the License, or (at your option) any later version.
!
!     This program is distributed in the hope that it will be useful,
!     but WITHOUT ANY WARRANTY; without even the implied warranty of
!     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
!     GNU General Public License for more details.
!
!     A copy of the GNU General Public License is available at
!     http://www.gnu.org/copyleft/gpl.html#SEC3
!     or by writing to the Free Software Foundation, Inc.,
!     59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
!
!
!   Authors
!
!    1.00: Dirk Rijnsdorp
!
!   Updates
!
!    1.00, August 2014: New subroutine
!
!   Purpose
!
!   Updates mask arrays for pressurized flow in water level and velocity points
!
!   Modules used
!
    use ocpcomm4
    use SwashCommdata3
    use m_genarr
    use SwashFlowdata
!
    implicit none
!
!   Local variables
!
    integer, save :: ient = 0 ! number of entries in this subroutine
    integer       :: m        ! loop counter
    integer       :: n        ! loop counter
    integer       :: nm       ! pointer to m,n
    !
    logical       :: STPNOW   ! indicates that program must stop
!
!   Structure
!
!   Description of the pseudo code
!
!   Source text
!
    if (ltrace) call strace (ient,'SwashPresFlow')
    !
    if ( oned ) then
       !
       ! loop over water level points
       !
       do m = mfu, ml
          !
          nm = kgrpnt(m,1)
          !
          if ( hs(nm) + flos(nm) < dps(nm) ) then
             !
             presp(nm) = 0
             !
          else
             !
             presp(nm) = 1
             !
          endif
          !
       enddo
       !
       ! exchange mask values for water level with neighbouring subdomains
       !
       call SWEXCHGI ( presp, kgrpnt, 1, 1 )
       if (STPNOW()) return
       !
       ! loop over u-points
       !
       do m = mf, ml
          !
          nm = kgrpnt(m,1)
          !
          if ( hu(nm) + flou(nm) < dpu(nm) ) then
             !
             presu(nm) = 0
             !
          else
             !
             presu(nm) = 1
             !
          endif
          !
       enddo
       !
       ! exchange mask values for u-velocity with neighbouring subdomains
       !
       call SWEXCHGI ( presu, kgrpnt, 1, 1 )
       if (STPNOW()) return
       !
    else
       !
       ! loop over water level points
       !
       do n = nfu, nl
          do m = mfu, ml
             !
             nm = kgrpnt(m,n)
             !
             if ( hs(nm) + flos(nm) < dps(nm) ) then
                !
                presp(nm) = 0
                !
             else
                !
                presp(nm) = 1
                !
             endif
             !
          enddo
       enddo
       !
       ! set to zero for permanently dry points
       !
       presp(1) = 0
       !
       ! exchange mask values for water level with neighbouring subdomains
       !
       call SWEXCHGI ( presp, kgrpnt, 1, 1 )
       if (STPNOW()) return
       !
       ! synchronize mask values for water level at appropriate boundaries in case of repeating grid
       !
       call periodici ( presp, kgrpnt, 1, 1 )
       !
       ! loop over u-points
       !
       do n = nfu, nl
          do m = mf, ml
             !
             nm = kgrpnt(m,n)
             !
             if ( hu(nm) + flou(nm) < dpu(nm) ) then
                !
                presu(nm) = 0
                !
             else
                !
                presu(nm) = 1
                !
             endif
             !
          enddo
       enddo
       !
       ! set to zero for permanently dry points
       !
       presu(1) = 0
       !
       ! exchange mask values for u-velocity with neighbouring subdomains
       !
       call SWEXCHGI ( presu, kgrpnt, 1, 1 )
       if (STPNOW()) return
       !
       ! synchronize mask values for u-velocity at appropriate boundaries in case of repeating grid
       !
       call periodici ( presu, kgrpnt, 1, 1 )
       !
       ! loop over v-points
       !
       do m = mfu, ml
          do n = nf, nl
             !
             nm = kgrpnt(m,n)
             !
             if ( hv(nm) + flov(nm) < dpv(nm) ) then
                !
                presv(nm) = 0
                !
             else
                !
                presv(nm) = 1
                !
             endif
             !
          enddo
       enddo
       !
       ! set to zero for permanently dry points
       !
       presv(1) = 0
       !
       ! exchange mask values for v-velocity with neighbouring subdomains
       !
       call SWEXCHGI ( presv, kgrpnt, 1, 1 )
       if (STPNOW()) return
       !
       ! synchronize mask values for v-velocity at appropriate boundaries in case of repeating grid
       !
       call periodici ( presv, kgrpnt, 1, 1 )
       !
    endif
    !
end subroutine SwashPresFlow
